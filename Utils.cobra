use System.Windows.Forms
use System.Drawing

class NajaUtils
    def promptForOpenFileName as String is shared
        return .promptForOpenFileName("Cobra files|*.cobra")
    
    def promptForOpenFileName(filter as String) as String is shared
        dlg = OpenFileDialog()
        dlg.filter = filter
        dlg.filterIndex = 1
        if dlg.showDialog == DialogResult.OK
            return dlg.fileName ? ""
        return ""
        
    def promptForSaveAsFileName(startFileName as String) as String is shared
        return .promptForSaveAsFileName(startFileName, "Cobra files|*.cobra")
        
    def promptForSaveAsFileName(startFileName as String,filter as String) as String is shared
        dlg = SaveFileDialog()
        dlg.fileName = startFileName
        dlg.filter = filter
        dlg.filterIndex = 1
        if dlg.showDialog == DialogResult.OK
            return dlg.fileName ? ""
        else
            return ""

	#from http://www.charp-examples.net/inputbox/
	def inputBox(title as String, promptText as String, value as inout String) as DialogResult is shared
		form = Form()
		label = Label()
		textBox = TextBox()
		buttonOk = Button()
		buttonCancel = Button()
		
		form.text = title
		label.text = promptText
		textBox.text = value
		
		buttonOk.text = "OK"
		buttonCancel.text = "Cancel"
		buttonOk.dialogResult = DialogResult.OK
		buttonCancel.dialogResult = DialogResult.Cancel
		
		label.setBounds(9,20,372,13)
		textBox.setBounds(12,36,372,20)
		buttonOk.setBounds(228,72,75,23)
		buttonCancel.setBounds(309,72,75,23)
		
		label.autoSize = true
		textBox.anchor = AnchorStyles(Left,Right,Top)
		buttonOk.anchor = AnchorStyles(Right,Bottom)
		buttonCancel.anchor = AnchorStyles(Right,Bottom)
		
		form.clientSize = Size(396,107)
		form.controls.addRange(@[label,textBox,buttonOk,buttonCancel])
		form.clientSize = Size(Math.max(300,label.right +10),form.clientSize.height)
		form.formBorderStyle = FormBorderStyle.FixedDialog
		form.startPosition = FormStartPosition.CenterScreen
		form.minimizeBox = false
		form.maximizeBox = false
		form.acceptButton = buttonOk
		form.cancelButton = buttonCancel
		
		dialogResult = form.showDialog
		value = textBox.text to !
		return dialogResult
